set _podlist = "${TCSHDIR}/etc/podlist.txt"

if ( -f ${_podlist} && -s ${_podlist} ) then
    set podcomplete = ( 'n@*@`\cat ' ${_podlist} '`@' )
else

    echo "podlist not found, attempting to generate..."
    ${TCSHDIR}/bin/perl ${TCSHDIR}/bin/podlist.pl

    if ( -f ${_podlist} && -s ${_podlist} ) then
        set podcomplete = ( 'n@*@`\cat ' ${_podlist} '`@' )
    else
        echo "podlist generation failed, looking for likely directory candidates"
        foreach d ( /usr/perl5 /usr/lib/perl5 /usr/local/perl5 /usr/local/lib/perl5 /mc/apps/perl/lib )
            if ( -d $d ) then
                set podcomplete=( 'n@*@`\ls -1 '$d'/5.*/pod | sed s%\\.pod.\*\$%%`@' )
                break
            endif
        end
    endif

endif

if ( ! $?podcomplete ) then
    echo "podlist generation failed, falling back to default"
            set podcomplete=( 'n@*@`\ls -1 /usr/lib/perl5/5.*/pod | sed s%\\.pod.\*\$%%`@' )
endif


complete perldoc    "${podcomplete}"

if ( $?podcomplete ) unset podcomplete
if ( $?_podlist    ) unset _podlist
if ( $?d           ) unset d

# vim: set et ff=unix ft=tcsh sts=0 sw=4 ts=4 : 
