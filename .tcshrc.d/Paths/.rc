set myDir = ( "${TCSHDIR}/Paths" )

cd ${myDir}


if ( $?debugON ) then
    echo "path at beginning = ${path}"

    echo "PWD:"
    pwd

endif

# Use this empty 'set' in case we don't already have a path
# variable set...
#
if ( ! ${?path} ) then
    set path
endif

if ( $?noglob ) then
    set noglobON = 1
endif

# {{{ common paths

if ( -d "common" && -r "common" ) then

    if ( $?noglob ) then
        unset noglob
    endif

    if ( $?debugON ) then
        echo "Fetching common path files begin..."
    endif

    set CommonPathFiles = ( common/* )

    if ( $?debugON ) then
        echo "Fetching common path files end..."
    endif


    if ( $?noglobON ) then
        set noglob
    endif

endif

if ( $?CommonPathFiles ) then
    set PathFiles = ( "${CommonPathFiles}" )
else
    set PathFiles
endif

# }}}

# {{{ ospaths
# 
# Read in the filenames containing paths specific to the
# current operating system
#
if ( $?tcshOS && -d "${tcshOS}" && -r "${tcshOS}" ) then

    if ( $?noglob ) then
        unset noglob
    endif

    if ( $?debugON ) then
        echo "Fetching os path files begin..."
        echo -n "PWD: "
        pwd
    endif

    set osPathFiles = ( ${tcshOS}/* )

    if ( $?debugON ) then
        echo "Fetching os path files end..."
    endif


    if ( $?noglobON ) then
        set noglob
    endif

endif

if ( $?osPathFiles ) then
    set -l PathFiles = ( ${osPathFiles} ${PathFiles} )
endif

if ( $?debugON ) then
    echo "PathFiles = ${PathFiles}"
endif

# }}}

# {{{ iterate path files

if ( $?PathFiles ) then

    set firstpass = 1

    foreach curPathFile ( $PathFiles )

        if ( $?debugON ) then
            echo curPathFile: $curPathFile
        endif

        if ( -r ${curPathFile} ) then
            if ( -s $curPathFile ) then

                foreach pathFileLine ( "`cat ${curPathFile}`" )

                    if ( $?pathFileLine ) then

                        # Using 'eval' in case we use variables like
                        # $HOME in the list of dirs.
                        #
                        set evalpath = ( `eval echo $pathFileLine` )

                        if ( $?debugON ) then
                            echo "Current evalpath = ${evalpath}"
                        endif

                        if ( -d ${evalpath} ) then

                            if ( $?firstpass ) then
                                set -f path = ( ${evalpath} ${path} )
                            else
                                set -l path = ( ${evalpath} ${path} )
                            endif

                        endif

                    else
                        if ( $?debugON ) then
                            echo "Current line from path file was empty"
                        endif
                    endif

                end

            else
                if ( $?debugON ) then
                    echo "Current path file was empty"
                    echo "cat file begin"
                    cat $curPathFile
                    echo "cat file end"
                endif
            endif
        endif

        unset firstpass

    end

else
    echo "ERROR: No path files found"
    echo "Setting default path"
    set path = ( /bin /usr/bin /usr/local/bin /sbin /usr/sbin /usr/local/sbin )
endif

# }}}

if ( $?debugON ) then
    echo "path at end = ${path}"
endif

# {{{ manpaths

if ( $?noglob ) then
    unset noglob
endif

if ( -r "common/.manpath" ) then
    foreach testmanpath ( "`cat common/.manpath`" )
        if ( -d "${testmanpath}" ) then
            if ( ! ${?manpath} ) then
                set manpath = ( "${testmanpath}" )
            else
                set manpath = ( "${manpath}:${testmanpath}" )
            endif
        endif
    end
endif

if ( -r "${tcshOS}/.manpath" ) then
    foreach testmanpath ( "`cat ${tcshOS}/.manpath`" )
        if ( -d "${testmanpath}" ) then
            if ( ! ${?manpath} ) then
                set manpath = ( "${testmanpath}" )
            else
                set manpath = ( "${manpath}:${testmanpath}" )
            endif
        endif
    end
endif


setenv manpath ${manpath}
setenv MANPATH ${manpath}


if ( $?debugON ) then
    echo "manpath = ${manpath}"
endif

# }}}

if ( -r ".source" ) then

    if ( $?debugON ) then
        echo "found .source file"
    endif

    source .source
endif

if ( -d ${TCSHDIR}/bin ) then
    set -f path = ( ${TCSHDIR}/bin ${path} )
endif

if ( $?noglobON ) then
    set noglob
endif

if ( $?CommonPathFiles ) unset CommonPathFiles
if ( $?osPathFiles     ) unset osPathFiles
if ( $?curPathFile     ) unset curPathFile
if ( $?PathFiles       ) unset PathFiles
if ( $?myDir           ) unset myDir
if ( $?pathFileLine    ) unset pathFileLine
if ( $?testmanpath     ) unset testmanpath

cd -

exit

# {{{


exit


# ---------------------------------------------------------------


foreach checkfirstpaths     \
(                           \
    /usr/local/bin          \
    /usr/bin                \
    $HOME/bin               \
)
    if ( -d "${checkfirstpaths}" ) then
        if ( ! ${?path} ) then
            set -f path = ( ${checkfirstpaths} )
        else
            set -f path = ( ${path} ${checkfirstpaths} )
        endif
    endif
end

# Other paths to check ...
#
foreach checkmorepaths      \
(                           \
    /bin                    \
    /opt/gnome/bin          \
    /opt/kde3/bin           \
    /root/bin               \
    /sbin                   \
    /usr/bin/X11            \
    /usr/contrib/bin        \
    /usr/games              \
    /usr/lib/mit/bin        \
    /usr/lib/mit/sbin       \
    /usr/lib/qt3/bin        \
    /usr/local/java/bin     \
    /usr/local/samba/bin    \
    /usr/local/sbin         \
    /usr/local/ssl/bin      \
    /usr/openwin/bin        \
    /usr/sbin               \
    /usr/X11R6/bin          \
)
    if ( -d "${checkmorepaths}" ) then
        if ( ! ${?path} ) then
            set -l path = ( ${checkmorepaths} )
        else
            set -l path = ( ${path} ${checkmorepaths} )
        endif
    endif
end

# }}}

# vim: ft=tcsh et ts=4 sts=4 sw=4
